[[init]]
iterators = distalv trajvar numclasses;

distalv = 1:3;
trajvar = 1:3;
numclasses = 2;

trajvarnames#trajvar = "wt" "ht" "wfl";
distalnames#distalv = "ldl" "hdl" "log_tg";

filename = "step3-pooled--distal-[[distalnames#distalv]]--[[trajvarnames#trajvar]]--outcome--[[numclasses]]-Classes.inp";

outputDirectory = "/proj/epi/CVDGeneNas/avonholle/ms-d2/data-prep-bf/mplus-templates/pooled/univ/step3";

[[/init]]

TITLE: Step 1. [[numclasses]] class quadratic growth model for CFA distal, BCH approach.

DATA: FILE = "../step1-pooled--[[trajvarnames#trajvar]]--outcome--[[numclasses]]-Classes.dat";

VARIABLE: 
NAMES = 
   [[trajvarnames#trajvar]]0 - [[trajvarnames#trajvar]]5    
   graf2 sex2  mk22 stillbf6
   hdl
   ldl
   log_tg
   sex graffar_i_ctr mk2 bf2
    BCHW1          
    BCHW2          
    ID;

MISSING=*;

CLASSES = uc([[numclasses]]);

IDVARIABLE is ID;

[[trajvar=2]]
USEVARIABLES =  
    bchw1 bchw2 
    [[distalnames#distalv]]
   sex2 mk22 bf2;
[[/trajvar=2]]
[[trajvar!=2]]
USEVARIABLES =  
    bchw1 bchw2
    [[distalnames#distalv]]
   sex2 graf2 mk22 bf2;
[[/trajvar!=2]]

TRAINING = BCHW1-BCHW2 (bch);

[[trajvar=2]]
  DEFINE:
  CENTER sex2 mk22 bf2(grandmean);
[[/trajvar=2]]
[[trajvar!=2]]
DEFINE:
  CENTER sex2 graf2 mk22 bf2 (grandmean);
[[/trajvar!=2]]


ANALYSIS:	
  TYPE = MIXTURE ;
  starts=0;
 
MODEL:

	%OVERALL%

[[trajvar=2]]
	[[distalnames#distalv]] on sex2 mk22 bf2;
[[/trajvar=2]]
[[trajvar!=2]]
	[[distalnames#distalv]] on sex2 graf2 mk22 bf2; 
	graf2;
[[/trajvar!=2]]

	! latent class variable and distal outcome
	! See section 3.2 of Asparouhov 2014 paper on bch method in mplus

[[trajvar=2]]
    %uc#1%

    [[[distalnames#distalv]]] (a1);
    [[distalnames#distalv]];

    %uc#2%

    [[[distalnames#distalv]]] (a2);
    [[distalnames#distalv]];

[[/trajvar=2]]

[[trajvar!=2]]
    %uc#1%

    [[[distalnames#distalv]]] (a1);
    [[distalnames#distalv]];
  	[[distalnames#distalv]] on sex2 graf2 bf2;
  	graf2;

    %uc#2%

    [[[distalnames#distalv]]] (a2);
    [[distalnames#distalv]];
  	[[distalnames#distalv]] on sex2 graf2  bf2;
  	graf2;
[[/trajvar!=2]]


MODEL TEST:
  0 = a1 - a2;

MODEL CONSTRAINT:
  new (diff1);
  diff1 = a1 - a2;


OUTPUT: 
    standardized sampstat;
