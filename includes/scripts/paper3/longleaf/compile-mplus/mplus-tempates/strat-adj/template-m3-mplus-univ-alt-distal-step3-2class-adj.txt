[[init]]
iterators = distalv trajvar numclasses sex snps;

distalv = 1:3;
trajvar = 1:3;
numclasses = 2;
sex = 1:2;
snps = 1:23;


trajvarnames#trajvar = "ht" "wfl" "wt";
distalnames#distalv = "ldl" "hdl" "log_tg";
snpnames#snps =    "rs12740374"
"rs629301" "rs1260326"
"rs78536982" "rs62436827"
"rs79236614" "rs28680850"
"rs4360309" "rs9282541" 
"rs964184" "rs1077835"
"rs5880" "rs1532624" 
"rs247617" "rs11076175"
"rs3764261" "rs72925845" 
"rs6511720" "rs7412" 
"rs4420638"
"un_grs_hdl" "un_grs_ldl" 
"un_grs_tg"
;

sexnames#sex = "Male" "Female";

filename = "step3-cubic--[[numclasses]]-Classes--[[trajvarnames#trajvar]]--outcome--distal-[[distalnames#distalv]]--[[sexnames#sex]]--snps-[[snpnames#snps]].inp";

outputDirectory = "/proj/epi/CVDGeneNas/avonholle/ms-d3/data-prep/mplus-templates/strat-adj/step3";

[[/init]]

TITLE: [[numclasses]] class cubic growth model for univariate distal with i and s assoc with distal allowed to vary across classes. 3rd step of 3-step process to estimate assn across the growth classes.

DATA: FILE = "../step1-[[numclasses]]-Classes--[[trajvarnames#trajvar]]--outcome--[[sexnames#sex]].dat";

VARIABLE: 

NAMES = [[trajvarnames#trajvar]]0 - [[trajvarnames#trajvar]]5 
graf2 gestage_  mk22
    HDL LDL LOG_TG
    rs12740374 rs629301 rs1260326 rs78536982 rs62436827
    rs79236614 rs28680850 rs4360309 rs9282541 rs964184
    rs1077835 rs5880 rs1532624 rs247617 rs11076175
    rs3764261 rs72925845 rs6511720 rs7412 rs4420638
    un_grs_hdl un_grs_ldl un_grs_tg 
    pc1-pc5
    sex mk2 graf
    w1 w2 
    c1 c2 uc
    id;

CLASSES = uc([[numclasses]]) ;

[[distalv!=3]]
USEVARIABLES = [[distalnames#distalv]] w1 w2 [[snpnames#snps]]
mk2 pc1-pc5;

MISSING = *; 

TRAINING = w1-w2 (bch);

DEFINE:
CENTER mk2 pc1-pc5 (grandmean);
[[/distalv!=3]]

[[distalv=3]] ! only use pc1-pc2 for log_tg. doesn't converge with more.
USEVARIABLES = [[distalnames#distalv]] w1 w2 [[snpnames#snps]]
mk2 pc1-pc2;
MISSING = *; 
TRAINING = w1-w2 (bch);
DEFINE:
CENTER mk2 pc1-pc2 (grandmean);
[[/distalv=3]]

ANALYSIS:	
  TYPE = MIXTURE ;
	STARTS = 0;

MODEL:
	%OVERALL%
[[distalv!=3]]
  [[distalnames#distalv]] on [[snpnames#snps]] mk2 pc1-pc5;
  uc on mk2 ;
[[/distalv!=3]]
[[distalv=3]]
  [[distalnames#distalv]] on [[snpnames#snps]] mk2 pc1-pc2;
  uc on mk2 ;
[[/distalv=3]]
  
[[numclasses=2]]
    %uc#1%
    [[distalnames#distalv]] on [[snpnames#snps]] (a1)
    mk2;
      [[distalnames#distalv]];
    %uc#2%
      [[distalnames#distalv]] on [[snpnames#snps]] (a2)
       mk2;
      [[distalnames#distalv]];
[[/numclasses=2]]

  
[[numclasses=3]]
    %uc#1%
      [[distalnames#distalv]] on [[snpnames#snps]] (a1)
     mk2;
      [[distalnames#distalv]];
    %uc#2%
      [[distalnames#distalv]] on [[snpnames#snps]] (a2)
     mk2;
      [[distalnames#distalv]];
    %uc#3%
      [[distalnames#distalv]] on [[snpnames#snps]] (a3)
     mk2;
      [[distalnames#distalv]];
[[/numclasses=3]]


MODEL TEST:

[[numclasses=2]]
    0 = a1-a2;
[[/numclasses=2]]

[[numclasses=3]]
    0 = a1-a2;
    0 = a1-a3;
    0 = a2-a3;
[[/numclasses=3]]


MODEL CONSTRAINT:

[[numclasses=2]]
    new(diff1);
    diff1 = a1-a2;
[[/numclasses=2]]

[[numclasses=3]]
    new(diff1-diff3);
    diff1 = a1-a2;
    diff2 = a1-a3;
    diff3 = a2-a3;
[[/numclasses=3]]

