[[init]]
iterators = distalv trajvar numclasses sex snps;

distalv = 1:3;
trajvar = 1:3;
numclasses = 2;
sex = 1:2;
snps = 1:13;


trajvarnames#trajvar = "ht" "wfl" "wt";
distalnames#distalv = "ldl" "hdl" "log_tg";

snpnames#snps =
    "rs12740374" "rs964184" "rs3135506" "rs11604424" "rs3741298" "rs247617" 
    "rs11076175" "rs7412" "rs1260326" "rs12678919"
      "un_grs_hdl"       
      "un_grs_ldl"       
      "un_grs_tg" 
;

sexnames#sex = "Male" "Female";

filename = "step3-cubic--[[numclasses]]-Classes--[[trajvarnames#trajvar]]--outcome--distal-[[distalnames#distalv]]--[[sexnames#sex]]--snps-[[snpnames#snps]].inp";

outputDirectory = "/proj/epi/CVDGeneNas/avonholle/ms-d3/data-prep/mplus-templates/strat/step3";

[[/init]]

TITLE: [[numclasses]] class cubic growth model for univariate distal with i and s assoc with distal allowed to vary across classes. 3rd step of 3-step process to estimate assn across the growth classes.

DATA: FILE = "../step1-[[numclasses]]-Classes--[[trajvarnames#trajvar]]--outcome--[[sexnames#sex]].dat";

VARIABLE: 

NAMES = [[trajvarnames#trajvar]]0 - [[trajvarnames#trajvar]]5 
    HDL LDL LOG_TG
    rs12740374 rs964184 rs3135506 rs11604424
    rs3741298 rs247617 rs11076175
    rs7412 rs1260326 rs12678919
    un_grs_hdl un_grs_ldl un_grs_tg 
    pc1-pc5
    sex mk2 graf
    w1 w2 
    c1 c2 uc
    id;

CLASSES = uc([[numclasses]]) ;

USEVARIABLES = [[distalnames#distalv]]  [[snpnames#snps]] w1 w2 ;
MISSING = *; 

TRAINING = w1-w2 (bch);

ANALYSIS:	
  TYPE = MIXTURE ;
	STARTS = 0;

MODEL:
	%OVERALL%

  [[distalnames#distalv]] on [[snpnames#snps]];
  
[[numclasses=2]]
    %uc#1%
      [[distalnames#distalv]] on [[snpnames#snps]] (a1);
    %uc#2%
      [[distalnames#distalv]] on [[snpnames#snps]] (a2);
[[/numclasses=2]]

  
[[numclasses=3]]
    %uc#1%
      [[distalnames#distalv]] on [[snpnames#snps]] (a1);
    %uc#2%
      [[distalnames#distalv]] on [[snpnames#snps]] (a2);
    %uc#3%
      [[distalnames#distalv]] on [[snpnames#snps]] (a3);
[[/numclasses=3]]


MODEL TEST:

[[numclasses=2]]
    0 = a1-a2;
[[/numclasses=2]]

[[numclasses=3]]
    0 = a1-a2;
    0 = a1-a3;
    0 = a2-a3;
[[/numclasses=3]]

MODEL CONSTRAINT:

[[numclasses=2]]
    new(diff1);
    diff1 = a1-a2;
[[/numclasses=2]]

[[numclasses=3]]
    new(diff1-diff3);
    diff1 = a1-a2;
    diff2 = a1-a3;
    diff3 = a2-a3;
[[/numclasses=3]]

