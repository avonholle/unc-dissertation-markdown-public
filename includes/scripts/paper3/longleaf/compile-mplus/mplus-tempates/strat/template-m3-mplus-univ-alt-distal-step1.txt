[[init]]
iterators = trajvar numclasses sex;

trajvar = 1:3;
numclasses = 2:3;
sex = 1:2;

trajvarnames#trajvar = "wt" "ht" "wfl";
sexnames#sex = "Male" "Female";

filename = "step1-cubic--[[numclasses]]-Classes--[[trajvarnames#trajvar]]--outcome--[[sexnames#sex]].inp";

outputDirectory = "/proj/epi/CVDGeneNas/avonholle/ms-d3/data-prep/mplus-templates/strat";

[[/init]]

TITLE: [[numclasses]] class cubic growth model for univariate distal with i and s assoc with distal allowed to vary across classes.

DATA: FILE = "/proj/epi/CVDGeneNas/avonholle/ms-d3/data-prep/aim3.dat";

VARIABLE: 
NAMES = id rs12740374 rs964184 rs3135506 rs11604424 rs3741298 rs247617 rs11076175
     rs7412 rs1260326 rs12678919 PC1 PC2 PC3 PC4 PC5 alpha0_wt beta0_wt beta1_wt
     beta0_ht beta1_ht beta0_wfl beta1_wfl hdl ldl tg GestatnlAg graffar_index Sex_f
     milkcode w_grs un_grs_hdl un_grs_ldl un_grs_tg graffar_i_ctr gestage_ctr mk2
     log_tg wt0 wt1 wt2 wt3 wt4 wt5 ht0 ht1 ht2 ht3 ht4 ht5 wfl0 wfl1 wfl2 wfl3 wfl4
     wfl5 sex; 
MISSING=.;


CLASSES = uc([[numclasses]]) ;

[[sex=1]]
  USEOBSERVATIONS = sex==1; ! subset for males
  [[/sex=1]]
  [[sex=2]]
  USEOBSERVATIONS = sex==0; ! subset for females
  [[/sex=2]]
  
USEVARIABLES = [[trajvarnames#trajvar]]0 - [[trajvarnames#trajvar]]5 ;

AUXILIARY = hdl ldl log_tg 
  rs12740374 rs964184 rs3135506 rs11604424 
  rs3741298 rs247617 rs11076175
  rs7412 rs1260326 rs12678919
  un_grs_hdl un_grs_ldl un_grs_tg pc1-pc5
  sex  mk2 graffar_i_ctr ;
  
idvariable id;

ANALYSIS:	TYPE = MIXTURE ;
	STARTS = 200 40;

MODEL:
	%OVERALL%
	i s q  | [[trajvarnames#trajvar]]0@0  [[trajvarnames#trajvar]]1@1.1 [[trajvarnames#trajvar]]2@2.1 [[trajvarnames#trajvar]]3@3.1 [[trajvarnames#trajvar]]4@4.1 [[trajvarnames#trajvar]]5@5.1;
	
    [[trajvarnames#trajvar]]0 (v0);
    [[trajvarnames#trajvar]]1 (v1);
    [[trajvarnames#trajvar]]2 (v2);
    [[trajvarnames#trajvar]]3 (v3);
    [[trajvarnames#trajvar]]4 (v4);
    [[trajvarnames#trajvar]]5 (v5);
    
    q@0;
    
    ! The AR(1). 
    ! See ~\GitHub\unc-dissertation-markdown-p2\includes\scripts\
    !   paper2\longleaf\compile-mplus\mplus-templates\ar1.xlsx
    ! for more details and example 6.17 in Mplus documentation
    ! and ar1.xlsx file in dropbox
    
    ! for first order corr;
    [[trajvarnames#trajvar]]1 with [[trajvarnames#trajvar]]0 (p1);
    [[trajvarnames#trajvar]]2 with [[trajvarnames#trajvar]]1 (p2);
    [[trajvarnames#trajvar]]3 with [[trajvarnames#trajvar]]2 (p3);
    [[trajvarnames#trajvar]]4 with [[trajvarnames#trajvar]]3 (p4);
    [[trajvarnames#trajvar]]5 with [[trajvarnames#trajvar]]4 (p5);
    
    
    ! for second order corr;
    [[trajvarnames#trajvar]]2 with [[trajvarnames#trajvar]]0 (p6);
    [[trajvarnames#trajvar]]3 with [[trajvarnames#trajvar]]1 (p7);
    [[trajvarnames#trajvar]]4 with [[trajvarnames#trajvar]]2 (p8);
    [[trajvarnames#trajvar]]5 with [[trajvarnames#trajvar]]3 (p9);


  MODEL CONSTRAINT:
  	NEW (corr);
  	    
    0 < v0;
    0 < v1;
    0 < v2;
    0 < v3;
    0 < v4;
    0 < v5; 
    
    ! first order;
  	p1 = corr * sqrt(v1) * sqrt(v0);
  	p2 = corr * sqrt(v2) * sqrt(v1);
  	p3 = corr * sqrt(v3) * sqrt(v2);
  	p4 = corr * sqrt(v4) * sqrt(v3);
  	p5 = corr * sqrt(v5) * sqrt(v4);

    ! second order;
  	p6 = corr**2 * sqrt(v2) * sqrt(v0);
  	p7 = corr**2 * sqrt(v3) * sqrt(v1);
  	p8 = corr**2 * sqrt(v4) * sqrt(v2);
  	p9 = corr**2 * sqrt(v5) * sqrt(v3);

!NOTE: model takes too long with addl terms for cov

SAVEDATA: 
  FILE = "step1-[[numclasses]]-Classes--[[trajvarnames#trajvar]]--outcome--[[sexnames#sex]].dat";
  SAVE = bchweights cprobabilities;
