[[init]]
iterators = trajvar numclasses;

trajvar = 1:3;
numclasses = 2:3;

trajvarnames#trajvar = "wt" "ht" "wfl";

filename = "step1-cubic--[[numclasses]]-Classes--[[trajvarnames#trajvar]]--outcome.inp";

outputDirectory = "/proj/epi/CVDGeneNas/avonholle/ms-d3/data-prep/mplus-templates/pooled-adj";

[[/init]]

TITLE: [[numclasses]] class cubic growth model for univariate distal with i and s assoc with distal allowed to vary across classes.

DATA: FILE = "/proj/epi/CVDGeneNas/avonholle/ms-d3/data-prep/aim3.dat";

VARIABLE: 
NAMES = id rs12740374 rs629301 rs1260326 rs78536982 rs62436827 rs79236614 rs28680850
     rs4360309 rs9282541 rs964184 rs1077835 rs5880 rs1532624 rs247617 rs11076175
     rs3764261 rs72925845 rs6511720 rs7412 rs4420638 PC1 PC2 PC3 PC4 PC5 alpha0_wt
     beta0_wt beta1_wt beta0_ht beta1_ht beta0_wfl beta1_wfl hdl ldl log_tg
     GestatnlAg graffar_index Sex_f milkcode w_grs un_grs_hdl un_grs_ldl un_grs_tg
     graffar_i_ctr gestage_ctr mk2 wt0 wt1 wt2 wt3 wt4 wt5 ht0 ht1 ht2 ht3 ht4 ht5
     wfl0 wfl1 wfl2 wfl3 wfl4 wfl5 sex;
     
MISSING=.;
CLASSES = uc([[numclasses]]);

IDVARIABLE is ID;

USEVARIABLES = [[trajvarnames#trajvar]]0 - [[trajvarnames#trajvar]]5 gestage_ctr
  sex2 graf2 mk22 ;

AUXILIARY = hdl ldl log_tg 
rs12740374 rs629301 rs1260326 rs78536982 rs62436827 rs79236614 rs28680850
     rs4360309 rs9282541 rs964184 rs1077835 rs5880 rs1532624 rs247617 rs11076175
     rs3764261 rs72925845 rs6511720 rs7412 rs4420638
  un_grs_hdl un_grs_ldl un_grs_tg pc1-pc5
  sex  mk2 graffar_i_ctr ;
  
DEFINE: 
  sex2 = sex;
  graf2 = graffar_i_ctr;
  mk22 = mk2;

ANALYSIS:	TYPE = MIXTURE ;
	STARTS = 100 20;

MODEL:
	%OVERALL%
	i s q | [[trajvarnames#trajvar]]0@0  [[trajvarnames#trajvar]]1@1.1 [[trajvarnames#trajvar]]2@2.1 [[trajvarnames#trajvar]]3@3.1 [[trajvarnames#trajvar]]4@4.1 [[trajvarnames#trajvar]]5@5.1;

    i on gestage_ctr sex2 graf2 mk22 ;
    s q on sex2;
    
    graf2; ! mention variable with missing values so all values included in analyses.
    ! not allowing class specific assn with confounders bc model specification issues

    [[trajvarnames#trajvar]]0 (v0);
    [[trajvarnames#trajvar]]1 (v1);
    [[trajvarnames#trajvar]]2 (v2);
    [[trajvarnames#trajvar]]3 (v3);
    [[trajvarnames#trajvar]]4 (v4);
    [[trajvarnames#trajvar]]5 (v5);
    
    q@0;
    
    ! The AR(1). 
    ! See ~\GitHub\unc-dissertation-markdown-p2\includes\scripts\
    !   paper2\longleaf\compile-mplus\mplus-templates\ar1.xlsx
    ! for more details and example 6.17 in Mplus documentation
    ! and ar1.xlsx file in dropbox
    
    ! for first order corr;
    [[trajvarnames#trajvar]]1 with [[trajvarnames#trajvar]]0 (p1);
    [[trajvarnames#trajvar]]2 with [[trajvarnames#trajvar]]1 (p2);
    [[trajvarnames#trajvar]]3 with [[trajvarnames#trajvar]]2 (p3);
    [[trajvarnames#trajvar]]4 with [[trajvarnames#trajvar]]3 (p4);
    [[trajvarnames#trajvar]]5 with [[trajvarnames#trajvar]]4 (p5);
    
    ! for second order corr;
    [[trajvarnames#trajvar]]2 with [[trajvarnames#trajvar]]0 (p6);
    [[trajvarnames#trajvar]]3 with [[trajvarnames#trajvar]]1 (p7);
    [[trajvarnames#trajvar]]4 with [[trajvarnames#trajvar]]2 (p8);
    [[trajvarnames#trajvar]]5 with [[trajvarnames#trajvar]]3 (p9);
    
    ! for third order corr;
    [[trajvarnames#trajvar]]3 with [[trajvarnames#trajvar]]0 (p10);
    [[trajvarnames#trajvar]]4 with [[trajvarnames#trajvar]]1 (p11);
    [[trajvarnames#trajvar]]5 with [[trajvarnames#trajvar]]2 (p12);    

    ! for fourth order corr;
!    [[trajvarnames#trajvar]]4 with [[trajvarnames#trajvar]]0 (p13);
!    [[trajvarnames#trajvar]]5 with [[trajvarnames#trajvar]]1 (p14);    
    
    ! for fifth order corr;
!    [[trajvarnames#trajvar]]5 with [[trajvarnames#trajvar]]0 (p15);    


  MODEL CONSTRAINT:
  	NEW (corr);
  	    
    0 < v0;
    0 < v1;
    0 < v2;
    0 < v3;
    0 < v4;
    0 < v5; 
    
    ! first order;
  	p1 = corr * sqrt(v1) * sqrt(v0);
  	p2 = corr * sqrt(v2) * sqrt(v1);
  	p3 = corr * sqrt(v3) * sqrt(v2);
  	p4 = corr * sqrt(v4) * sqrt(v3);
  	p5 = corr * sqrt(v5) * sqrt(v4);

    ! second order;
  	p6 = corr**2 * sqrt(v2) * sqrt(v0);
  	p7 = corr**2 * sqrt(v3) * sqrt(v1);
  	p8 = corr**2 * sqrt(v4) * sqrt(v2);
  	p9 = corr**2 * sqrt(v5) * sqrt(v3);

    ! third order;
  	p10 = corr**3 * sqrt(v3) * sqrt(v0);
  	p11 = corr**3 * sqrt(v4) * sqrt(v1);
  	p12 = corr**3 * sqrt(v5) * sqrt(v2);  ! exclude v5. takes too long to compute;

    ! fourth order;
!  	p13 = corr**4 * sqrt(v4) * sqrt(v0);
!  	p14 = corr**4 * sqrt(v5) * sqrt(v1);  	
  	
    ! fifth order;
!  	p15 = corr**5 * sqrt(v5) * sqrt(v0);

SAVEDATA: 
  FILE = "step1-[[numclasses]]-Classes--[[trajvarnames#trajvar]]--outcome.dat";
  SAVE = bchweights cprobabilities;
