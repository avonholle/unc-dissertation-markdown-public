[[init]]
iterators = distalv trajvar numclasses snps;

distalv = 1:3;
trajvar = 1:3;
numclasses = 2;
snps = 1:13;

trajvarnames#trajvar = "ht" "wt" "wfl" ;
distalnames#distalv = "ldl" "hdl" "log_tg";
snpnames#snps =
    "rs12740374" "rs964184" "rs3135506" "rs11604424" "rs3741298" "rs247617" 
    "rs11076175" "rs7412" "rs1260326" "rs12678919"
      "un_grs_hdl"       
      "un_grs_ldl"       
      "un_grs_tg" 
;

filename = "step3-cubic--[[numclasses]]-Classes--[[trajvarnames#trajvar]]--outcome--distal-[[distalnames#distalv]]--snps-[[snpnames#snps]].inp";

outputDirectory = "/proj/epi/CVDGeneNas/avonholle/ms-d3/data-prep/mplus-templates/pooled-adj/step3";

[[/init]]

TITLE: [[numclasses]] class cubic growth model for univariate distal with i and s assoc with distal allowed to vary across classes. 3rd step of 3-step process to estimate mean lipids across the growth classes.

DATA: FILE =  "../step1-[[numclasses]]-Classes--[[trajvarnames#trajvar]]--outcome.dat";

VARIABLE: 
NAMES = [[trajvarnames#trajvar]]0 - [[trajvarnames#trajvar]]5 
graf2 gestage_ sex2 mk22
        HDL LDL LOG_TG
    rs12740374 rs964184 rs3135506 rs11604424
    rs3741298 rs247617 rs11076175
    rs7412 rs1260326 rs12678919
    un_grs_hdl un_grs_ldl un_grs_tg 
    pc1-pc5
    sex mk2 graffar_i_ctr 
    bchw1 bchw2 c1 c2 uc
     id;

CLASSES = uc([[numclasses]]) ;

[[trajvar!=2]]
USEVARIABLES = [[distalnames#distalv]] bchw1 bchw2 [[snpnames#snps]]
sex2 mk2 pc1-pc5;
[[/trajvar!=2]]
[[trajvar=2]] ! pc not working with wt, 
! error message: 
! WARNING:  THE SAMPLE VARIANCE OF PC1 IN CLASS 1 IS  0.000.
USEVARIABLES = [[distalnames#distalv]] bchw1 bchw2 [[snpnames#snps]]
sex2 mk2;
[[/trajvar=2]]

MISSING = *; 

TRAINING = BCHW1-BCHW2 (bch);

[[trajvar!=2]]
DEFINE:
CENTER mk2 pc1-pc5 (GRANDMEAN);
[[/trajvar!=2]]
[[trajvar=2]]
DEFINE:
CENTER mk2  (GRANDMEAN);
[[/trajvar=2]]

ANALYSIS:	
  TYPE = MIXTURE ;
  starts=0;

MODEL:
	%OVERALL%

[[trajvar!=2]]
  [[distalnames#distalv]] on [[snpnames#snps]]  mk2 sex2 pc1-pc5;
[[/trajvar!=2]]
[[trajvar=2]]
  [[distalnames#distalv]] on [[snpnames#snps]]  mk2 sex2 ;
[[/trajvar=2]]
  uc on mk2 sex2;
  
[[numclasses=2]]
    %uc#1%
    [[distalnames#distalv]] on [[snpnames#snps]] (a1)
    sex2 mk2;
      [[distalnames#distalv]];
    %uc#2%
      [[distalnames#distalv]] on [[snpnames#snps]] (a2)
      sex2 mk2;
      [[distalnames#distalv]];
[[/numclasses=2]]

  
[[numclasses=3]]
    %uc#1%
      [[distalnames#distalv]] on [[snpnames#snps]] (a1)
    sex2 mk2;
      [[distalnames#distalv]];
    %uc#2%
      [[distalnames#distalv]] on [[snpnames#snps]] (a2)
    sex2 mk2;
      [[distalnames#distalv]];
    %uc#3%
      [[distalnames#distalv]] on [[snpnames#snps]] (a3)
    sex2 mk2;
      [[distalnames#distalv]];
[[/numclasses=3]]


MODEL TEST:

[[numclasses=2]]
    0 = a1-a2;
[[/numclasses=2]]

[[numclasses=3]]
    0 = a1-a2;
    0 = a1-a3;
    0 = a2-a3;
[[/numclasses=3]]


MODEL CONSTRAINT:

[[numclasses=2]]
    new(diff1);
    diff1 = a1-a2;
[[/numclasses=2]]

[[numclasses=3]]
    new(diff1-diff4);
    diff1 = a1-a2;
    diff2 = a1-a3;
    diff3 = a2-a3;
[[/numclasses=3]]

