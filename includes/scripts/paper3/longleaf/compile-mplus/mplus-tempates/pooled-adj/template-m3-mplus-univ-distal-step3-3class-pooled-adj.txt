[[init]]
iterators = distalv trajvar numclasses snps;

distalv = 1:3;
trajvar = 1:2;
numclasses = 3;
snps = 1:25;

trajvarnames#trajvar = "wt" "wfl" ;
distalnames#distalv = "ldl" "hdl" "log_tg";

snpnames#snps =
"rs1077835"  "rs11076175" "rs1260326"  "rs12740374" "rs1532624"
"rs247617"   "rs28680850" "rs3764261"  "rs4360309"  "rs4420638" 
"rs5880"  "rs62436827" "rs629301"   "rs6511720"  "rs6511720"  
"rs72925845" "rs7412"     "rs78536982" "rs79236614" "rs9282541" 
"rs964184"   "rs964184"  
      "UN_GRS_Hdl"       
      "UN_GRS_Ldl"       
      "UN_GRS_Tg" 
;

filename = "step3-cubic--[[numclasses]]-Classes--[[trajvarnames#trajvar]]--outcome--distal-[[distalnames#distalv]]--snps-[[snpnames#snps]].inp";

outputDirectory = "/proj/epi/CVDGeneNas/avonholle/ms-d3/data-prep/mplus-templates/pooled-adj/step3";

[[/init]]


TITLE: [[numclasses]] class cubic growth model for univariate distal with i and s assoc with distal allowed to vary across classes. 3rd step of 3-step process to estimate mean lipids across the growth classes.

DATA: FILE =  "../step1-[[numclasses]]-Classes--[[trajvarnames#trajvar]]--outcome.dat";

VARIABLE: 
NAMES = [[trajvarnames#trajvar]]0 - [[trajvarnames#trajvar]]5 
graf2 gestage_ sex2 mk22 
        HDL LDL LOG_TG
    rs12740374 rs629301 rs1260326 rs78536982 rs62436827
    rs79236614 rs28680850 rs4360309 rs9282541 rs964184
    rs1077835 rs5880 rs1532624 rs247617 rs11076175
    rs3764261 rs72925845 rs6511720 rs7412 rs4420638
    un_grs_hdl un_grs_ldl un_grs_tg pc1-pc5
    sex  mk2 graffar_i_ctr 
    bchw1 bchw2 bchw3 c1 c2 c3 uc
    id;
      
CLASSES = uc([[numclasses]]) ;

USEVARIABLES = [[distalnames#distalv]] bchw1 bchw2 bchw3  [[snpnames#snps]]
sex2 mk2 pc1-pc5;

MISSING = *; 

TRAINING = BCHW1-BCHW3 (bch);

DEFINE:
CENTER sex2 mk2 pc1-pc5 (GRANDMEAN);

ANALYSIS:	
  TYPE = MIXTURE ;
  starts=0;

MODEL:
	%OVERALL%

  [[distalnames#distalv]] on [[snpnames#snps]]  mk2 sex2  pc1-pc5;
  uc on mk2 sex2 ;

  
[[numclasses=2]]
    %uc#1%
    [[distalnames#distalv]] on [[snpnames#snps]] (a1)
    sex2 mk2;
      [[distalnames#distalv]];
    %uc#2%
      [[distalnames#distalv]] on [[snpnames#snps]] (a2)
       sex2 mk2;
      [[distalnames#distalv]];
[[/numclasses=2]]

  
[[numclasses=3]]
    %uc#1%
    [[distalnames#distalv]] on [[snpnames#snps]] (a1)
     sex2 mk2;
      [[distalnames#distalv]];
    %uc#2%
      [[distalnames#distalv]] on [[snpnames#snps]] (a2)
       sex2 mk2;
      [[distalnames#distalv]];
    %uc#3%
      [[distalnames#distalv]] on [[snpnames#snps]] (a3)
      sex2 mk2;
      [[distalnames#distalv]];
[[/numclasses=3]]

MODEL TEST:

[[numclasses=2]]
    0 = a1-a2;
[[/numclasses=2]]

[[numclasses=3]]
    0 = a1-a2;
    0 = a1-a3;
    0 = a2-a3;
[[/numclasses=3]]

MODEL CONSTRAINT:

[[numclasses=2]]
    new(diff1);
    diff1 = a1-a2;
[[/numclasses=2]]

[[numclasses=3]]
    new(diff1-diff3);
    diff1 = a1-a2;
    diff2 = a1-a3;
    diff3 = a2-a3;
[[/numclasses=3]]

